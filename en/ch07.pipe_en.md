# Pipe

## 管道操作符不是组合函数

在函数式编程的社区中, 函数组合非常的重要. 组合函数是一个高阶函数, 它接受多个函数,
返回一个新的函数, 例如 `fun=compose(f,g,k)`, 那么 `fun(x,y,z)` 就等价于
`f(g(k(x,y,z)))`. 为了与思维的习惯一致, 人们还定义了反序的组合函数, 叫做 `pipe` 函数.

在 Elixir 中没有内置的 `compose` 或 `pipe` 函数, 但是内置了管道操作符 `|>`.
我一度认为管道操作符 `|>` 就是 Elixir 的 `pipe` 函数. 但是这是一个幻觉.
管道操作符 `|>` 更像 shell 的管道而不是函数式编程中的 `pipe` 函数.

至少 Elixir 编译器不会为我们做函数组合的优化.

函数式编程中, 很多语言, 会对函子的 map 操作做优化. 比如:
`[1,2,3].map(f).map(f2)` 编译器会优化为 `[1,2,3].map(compse(f,f2))`,
以提高程序的执行效率.

在 Elixir 中, 相应的代码是这样的: `[1,2,3] |> Enum.map(f)|> Enum.map(f2)`.
这样的代码, 如果有优化的, 最后应该等价于 `[1,2,3]|> Enum.map(&f2.(f.(&1)))`.
但是实际的情况并不是这样的.

例如下面的代码:

```elixir
fun1 = fn v -> v |> IO.inspect(label: "+1") |> then(&(&1 + 1)) end
fun2 = fn v -> v |> IO.inspect(label: "-1") |> then(&(&1 - 1)) end
range = 1..5

range
|> Enum.map(fun1)
|> Enum.map(fun2)
|> IO.inspect(label: "result of map chain")
```

控制台的输出为:

```
+1: 1
+1: 2
+1: 3
+1: 4
+1: 5
-1: 2
-1: 3
-1: 4
-1: 5
-1: 6
result of map chain: [1, 2, 3, 4, 5]
```

从控制台的输出中, 可以观察到, 所有的 `+1` 输出结束后, 才开始 `-1` 的输出.
管道操作符 `|>` 连接起来的多个 `Enum.map` 并没有被合并为一个.
只有对 `fun1` 的迭代全部完成后, 对 `fun2` 调用才开始执行.
但如果把 `fun2` 和 `fun1` 组合成一个新的函数, 像下面的代码这样:

```elixir
range
|> Enum.map(&fun2.(fun1.(&1)))
|> IO.inspect(label: "result of compose")
```

那么控制台的输出应该是 `+1` `-1` 交替出现.

因此可见, Elixir 内置的管道操作符 `|>` 不是函数式编程的 `pipe` 函数.

可是手动的组合函数总是比较的麻烦, 写一个函数来帮助我们完成函数的组合也不是难事,
但是 Elixir 社区中, 因为命名函数不是一等公民, 所以函数组合的语法并不常用.
大家最习惯的其实还是使用管道操作符 `|>`, 所以如果管道操作符 `|>` 能对 `Enum.map`
做优化, 那么应该能提升代码的运行效率.

非常幸运, 管道操作符 `|>` 在 Elixir 中并不是特殊表单, 因此可以定义自己的管道操作符
`|>`.

Elixir 源码中定义的管道操作符 `|>` 可以作为我们工作的起点.
我们可以在
[`kernel.ex`](https://github.com/elixir-lang/elixir/blob/9a4d10e702f33d2fa47718cde05375b506b4a3d6/lib/elixir/lib/kernel.ex#L4073)
中发现其定义:

```elixir
defmacro left |> right do
  [{h, _} | t] = Macro.unpipe({:|>, [], [left, right]})

  fun = fn {x, pos}, acc ->
    Macro.pipe(acc, x, pos)
  end

  :lists.foldl(fun, h, t)
end
```

不用特别的熟悉 Elixir 的宏, 只需要理解最后的 `:lists.foldl/3`,
我们就能大概理解管道操作符 `|>` 的原理了, 其实与在 Javascript 中定义
`pipe` 函数的算法一样.

上面的代码中, `t` 就是管道操作符 `|>` 的所有的右操作数 (函数) 组成的列表,
也就是所有待组合的 **函数调用** 的列表.

我们自己定义的管道操作符 `|>` 所作的优化, 本质就是对函数调用列表 `t` 合并同类项.
具体来说就是, 识别连续相邻的 `Enum.map(f1)`, `Enum.map(f2)` ...,`Enum.map(fn)`,
并把他们合并成为一个 `Enum.map(composed_fun)`.

首先我们定义一个 `Optimizer` 行为, 这个这个行为只有一个函数, 就是优化函数:
`optimize(t,list)`.

所以如此, 是因为, 虽然现在我们只是完成 `Enum.map/2` 相关的实现,
但是对于管道操作符 `|>` 的右操作数, 如果将来发现, 除 `Enum.map/2` 组成的链条外,
还有其他的情形可以优化, 那么我们只需要对新情形实现 `Optimizer` 行为就好了.

定义 `Optimizer` 行为, 让我们的代码更容易组织.

## 优化器协议
```elixir
defmodule Optimizer do
  @callback optimize(t :: any, ast_list :: List.t()) :: List.t()

  defmacro __using__(_opts) do
    quote do
      import Kernel, except: [|>: 2]
      import Optimizer.Pipe, only: [|>: 2]
    end
  end
end
```

第 2 行我们定义了优化器的接口.
而 `__using__/1` 的定义可以看作是优化器对外提供的 API.
优化器的使用者, 只需要 `use Optimizer` 就可以了使用优化器了,
不用关心优化器内部的实现.

## 对 Enum.map 优化
现在让我们来定义针对 `Enum.map/2` 的优化.
我把有关优化的代码都放在 `optimizer` 目录下,
为 `Enum.map/2` 做优化的模块, 命名为 `Optimizer.EnumMap`.

像我们上面分析的那样, `Optimizer.EnumMap` 要做的事情, 就是把由管道操作符 `|>`,
链接起来的连续的多个 `Enum.map(fun)` 表达式合并为一个 `Enum.map(funs)`.
这里由 `reduce_map/1` 函数完成具体的工作. `reduce_map` 函数分两步完成工作:
1. 对所有函数调用表达式进行分组, 连续的 `Enum.map(fun)` 表达式分为一组,
其他的表达式, 单独为一组. 由 `group_function_calls/1` 完成.
2. 把分组后的 `Enum.map(fun)` 表达式抽象语法树, 
合并为一个 `Enum.map(composed_fun)`. 由 `tranceform_group/1`.
```elixir
defmodule Corner.Optimizer.EnumMap do
  alias Corner.Optimizer
  @behaviour Optimizer

  @impl Optimizer
  def optimize(__MODULE__, list) do
    reduce_map(list)
  end

  defp reduce_map(ast_list) do
    ast_list
    |> group_function_calls()
    |> Enum.map(&tranceform_group/1)
  end
  #...
end
```
`group_function_calls` 就是对函数调用表达式的抽象语法树列表做迭代.
对于函数调用 `fun_call`, 如果它是 `Enum.map(fun)` 的抽象语法树,
那么它应该就是被分组的内容, 首先把它使用列表包裹, 以表示这是一个分组,
然后把列表添加到汇聚结果 `acc` 的头部.
如果它不是 `Enum.map(fun)`, 那么直接加入到 `acc` 头部就好了.

当汇聚结果 `acc` 其中由内容了, 待检查的 `fun` 符合要求, 
而且汇聚结果的最后一个元素是一个列表话, 那么我们需要继续向列表中添加新的元素.
如果汇聚结果的最后一个元素不是一个列表. 那么需要向汇聚结果中添加新的分组列表.

当 `fun` 不是 `Enum.map(fun)` 的时候, 只要把 `fun` 添加到汇聚结果中, 就可以了.

这样操作后, 函数调用的顺序全部颠倒了, 所以使用 `Enum.reverse/1`
对函数调用列表完成重排. 
```elixir
defp group_function_calls(fun_call_list) do
  for fun_call <- fun_call_list, reduce: [] do
    [] = acc ->
      if is_map_call(fun_call) do
        [[fun_call] | acc]
      else
        [fun_call | acc]
      end
    [ele | rest] = acc ->
      fun_is_map? = is_map_call(fun_call)
      cond do
        fun_is_map? and is_list(ele) ->
          [[fun_call | ele] | rest]
        fun_is_map? ->
          [[fun_call] | acc]
        true ->
          [fun_call | acc]
      end
  end
  |> Enum.reverse()
end
```

`is_map_call/1` 用来检查抽象语法树是否对应表达式 `Enum.map(fun)`.

```elixir
defp is_map_call({{{:., _, [{:__aliases__, _, [:Enum]}, :map]}, _, _}, 0}),
  do: true
defp is_map_call(_), do: false
```
`tranceform_group/1` 把分组的 `Enum.map(fun)` 合并为一个
`Enum.map(composed_fun)`.
这也是分三步完成, 第一步提取 `Enum.map(fun)` 表达式中的 `fun`.
这由 `get_fun/1` 函数完成.
第二个步组合提取函数. 最后一步把组合函数 `composed_fun` 转为为
`Enum.map(composed_fun)`. 

其他非 `Enum.map(fun)` 的函数调用, 不做处理直接返回.

```elixir
defp tranceform_group(map_calls) when is_list(map_calls) do
  composed_fun =
    map_calls
    |> Enum.map(&get_fun/1)
    |> compose()

  {{{:., [], [{:__aliases__, [], [:Enum]}, :map]}, [], 
   [composed_fun]}, 
   0}
end
defp tranceform_group(other_ast), do: other_ast
```
`get_fun/1` 函数非常简单, 我们只是通过模式匹配来提取我们需要的就可以了.
```elixir
defp get_fun(
       {{
          {:., _, [{:__aliases__, [], [:Enum]}, :map]},
          _,
          [fun]
        }, 0}
     ) do
  fun
end
```
最后是组合函数. `compose/1-2`.
```elixir
defp compose(list, acc \\ nil)

defp compose([fun | funs], nil) do
  acc =
    quote do
      unquote(fun).()
    end
  compose(funs, acc)
end

defp compose([fun | funs], acc) do
  acc =
    quote do
      unquote(fun).() |> unquote(acc)
    end
  compose(funs, acc)
end

defp compose([], acc) do
  quote do
    fn v -> v |> unquote(acc) end
  end
end
```
分组后函数的列表的顺序与它们在管道操作符中出现的顺序已经完成了反转.
对与列表 `[fun_n, ... fun_2, fun_1]`. 这里每一个函数都是一元函数.
最终要转化为 `fn v -> v |> fun_1.() |> fun_2.() |> ... |> fun_n.() end`.
第一个分句完成, 最后 `fun_n.()`, 第二个分句完成
`fun_1.() |> fun_2.() ..|> fun_n.()`.
而最后一个数据完成 `fn -> v |> ... end` 的操作.

前面说过, 要自定义管道操作符 `|>`, 现在是兑现的时候了.

## 管道操作符
```elixir
defmodule Corner.Optimizer.Pipe do
  import Kernel, except: [|>: 2]
  alias Corner.Optimizer.{EnumMap}
  @optimizer [EnumMap]
  defmacro left |> right do
    [{h, _} | t] = Macro.unpipe({:|>, [], [left, right]})

    fun = fn {x, pos}, acc ->
      Macro.pipe(acc, x, pos)
    end

    t = Enum.reduce(@optimizer, t, &apply(&1, :optimize, [&1, &2]))
    :lists.foldl(fun, h, t)
  end
end
```

当定义了更多的优化器的时候, 只需要修改上面代码的第 3, 4 两行代码, 就可以了.

上面的代码, 大部分都是照搬 Elixir 的 Kernel 模块中管道操作符 `|>` 的定义,
只是增加了第 12 行的内容, 而它做的工作, 依次的调用我们定义的优化器.

现在来看看, 代码是不能按照我们的预期工作:

```elixir
use Corner.Optimizer
add_one = fn v ->
  IO.puts("+1")
  v + 1
end

sub_one = fn v ->
  IO.puts("-1")
  v - 1
end

1..5
|> Enum.map(add_one)
|> Enum.map(sub_one)
|> IO.inspect(label: "after optimizer")
```
控制台中输出为:
```
+1
-1
+1
-1
+1
-1
+1
-1
+1
-1
after optimizer: [1, 2, 3, 4, 5]
```
## 基准测试
看起来, 功能测试通过了, 但是我们毕竟是做优化的, 我们的代码真的能提升代码的执行效率吗?
这还需要实际测试的.

```elixir
defmodule OptimizeBenchmarkTest do
  use ExUnit.Case, async: true
  @range 1..1000

  def fun1(up \\ 10) do
    for _ <- 1..up do
      @range
      |> Enum.map(&(&1 + 1))
      |> Enum.map(&(&1 - 1))
    end
  end

  def fun2(up \\ 10) do
    use Corner.Optimizer

    for _ <- 1..up do
      @range
      |> Enum.map(&(&1 + 1))
      |> Enum.map(&(&1 - 1))
    end
  end

  test "Optimizer of |> for Enum.map" do
    arg = [10000]
    task1 = Task.async(fn -> :timer.tc(&fun1/1, arg) end)
    task2 = Task.async(fn -> :timer.tc(&fun2/1, arg) end)

    [{t, v}, {t1, v1}] = Task.await_many([task1, task2], 60_000)
    IO.puts("speed up #{(t - t1) / t1 * 100}%.")
    assert v == v1
  end
end
```

最初, 我是使用 LiveBoook 来探索 Elixir 的.
在 LiveBook 中, 运行这个测试的后, 其中有一次, 代码的输出是这样的:

> speed up -1.5641347109939043%.

这就尴尬了😒. 优化后的代码不是提速了, 反而变慢了 1.5 个百分点. 可是为什么会这样啊?

这是因为 LiveBook 代码 Cell 本质上是解释执行的, 就像不能以代码在 IEx
中的运行时间来衡量代码的效率一样, 我们也不能用 LiveBook 的 Cell 
中代码的执行时间来衡量代码的效率.

这个优化的实现, 其实并不是那么的复杂, 为什么 Elixir 编译器不这样做呢?
这里有两个原因. 第一原因, 就是 Elixir 标准库中有 `Stream` 模块,
文档中, 明确提到 `Stream.map` 会组合函数的. 
也就是说, Elixir 是以 `Stream.map` 来完成函数组合的工作的.

对于上面的组合代码, 使用 `Stream` 替换了 `Enum`, 就获得了相同的结果.

```elixir
1..5
|> Stream.map(add_one)
|> Stream.map(sub_one)
|> Enum.to_list()
|> IO.inspect(label: "result of map chain")
```

所以, 当我看到测试在 LiveBook 效率没有提升, 甚至还下降的时候,
我一度怀疑, 自己是不是解决了一个错误的问题?

答案是: 不是的. 这个工作依旧是有意义的.

就像上面展示的那样, 理论上绝对不有应该效率下降的优化后的代码, 测试的时候, 
有时居然会下降 1.5%. 那么 Stream 版本呢? 会比我们的的代码效率更高吗?
总是优与多个 `Enum.map` 串联吗?
```elixir
defmodule BenchmarkStreamTest do
  use ExUnit.Case, async: true
  @range 1..1000

  def fun1(up \\ 10) do
    for _ <- 1..up do
      @range
      |> Enum.map(&(&1 + 1))
      |> Enum.map(&(&1 - 1))
    end
  end

  def fun2(up \\ 10) do
    for _ <- 1..up do
      @range
      |> Stream.map(&(&1 + 1))
      |> Stream.map(&(&1 - 1))
      |> Enum.to_list()
    end
  end

  test "Enum.map vs Stream.map" do
    arg = [10000]
    task1 = Task.async(fn -> :timer.tc(&fun1/1, arg) end)
    task2 = Task.async(fn -> :timer.tc(&fun2/1, arg) end)

    [{t, v}, {t1, v1}] = Task.await_many([task1, task2], 60_000)
    IO.puts("speed up #{(t - t1) / t * 100}%.")
    assert v == v1
  end
end
```

这次在 LiveBook 中运行的结果更加糟糕, 和 `Enum.map` 相比,
`Stream.map` 版本的代码效率下降了 100% 还多.

1%~2% 的效率差异我们还能解释为代码执行过程中偶然因素带来的,
但 `Stream.map` 和 `Enum.map` 的巨大差异绝对不能归结为偶然.
`Stream.map` 虽然完成了的函数的组合, 但是却并没有提升效率.

Elixir 的 `Enum.map` 不组合函数的第二个原因是, Elixir 不是一个纯的函数编程语言.
传递给 `Enum.map` 的函数不保证没有副作用的. 而理论上来说,
只有当传递给 `Enum.map` 的函数是纯函数的时候, 才可以组合函数.
但是要在编译时判断一个函数中是否有副作用, 这基本上做不到,
所以 Elixir 标准库中的管道操作符 `|>` 并不对 `Enum.map` 做函数的组合.

我们的优化器, 忽略了函数中的副作用而强行对函数做了组合.
把优化器做测试的代码, 保存到文件中, 使用 `mix test` 命令来重新测试, 
这次发现, 使用优化器的代码, 效率有了 30%~40% 的效率提升.

手动组合功能一样的函数, 其效率和使用这里定义的优化器的一样.
在不增加工作量的情况下, 效率提升 30%~40%, 从这个角度来看, 这个工作还是算有意义吧?
