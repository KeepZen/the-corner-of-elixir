# 第八章 错误处理

## 错误分类

Erlang 中把错误分成 4 类.

1. 编译时错误, 也就是语法错误
2. 逻辑错误, 漏洞或者更常见的是说法 bug.
3. 运行时错误, 运行时产生的错误
4. 产生的错误, 也就是写代码中故意产生的错误.

前两种错误, 发生在语言之外的, 在代码层面没法对其做处理.

后两种错误又叫做异常. 代码层面能处理的其实都是异常.

异常以引发异常的机制和函数不同, 又可以分成三类:

1. 错误(`:error`), 运行时的错误, 例如 `1 + :a` 或者有 `:erlang.error()` 引发的.
2. 退出(`:exit`), 由 `:erlang.exit/1-2-3` 引发, 用来使进程退出执行.
3. 抛出(`:throw`), 由 `:erlang.throw` 引发.

Erlang 中用 `catch` 关键字来处理这些错误.
而 Elixir 中, 除了 `catch` 外, 还从 Ruby 中引入了新的 `rescue` 保留字.
那么自然的就有一个问题, `catch` 和 `rescue` 如何分工呢?

按照 [Elixir 文档](https://hexdocs.pm/elixir/1.13.1/Kernel.SpecialForms.html#try/1)
的说法, `rescue` 用来处理异常, 而 `catch` 可以用来捕获 `throw` 值和 `exit` 信号.
(The `rescue` clause is used to handle exceptions while the `catch` clause can
be used to catch thrown values and exits.)

但是什么是异常呢? Elixir文档中并没有明确的给出定义, 但是
[Erlang 文档](https://www.erlang.org/doc/reference_manual/errors.html#exceptions)
中有清晰的解释.

如果 Elixir 中的异常和 Erlang 中的是一个概念话,
那么我对 `rescue` 和 `catch` 的分工的理解,
就成了: `rescue` 能处理所有三种类型的异常, 而 `catch` *只能* 处理 `throw` 和
`exit` 引发的异常. 但是**这样的理解是错误的**.

例如下面的代码,`rescue` 在这里并没有把我们从 `throw` 引发的错误中拯救出来.

```elixir
try do
  throw(1)
rescue
  v -> IO.inspect(v)
end
```

使用 Erlang 对异常的分类, `rescue` 实际上只能处理错误 `error` 类型错误.
Elixir 文档中也的确有提到 `rescue` 对 Erlang 的错误的处理,
所有的 Erlang 错误都被转化为 `ErlangError`.
这可以看作是一种暗示: Elixir 中异常对应于 Erlang 的 `:error` 类型的错误.

理解了这一点, 对于 `catch` 的分工描述才能有正确的理解.
在 Erlang 中, 本来就是用`try...catch` 来处理所有错误的;
Elixir 中依旧可以使用 `catch` 来处理全部的三种类型的错误, 也就是说,
在 Elixir 中 `catch` 并没有退化. 而 Elixir 中增加的保留字 `resuce`,
只是针对 `:error` 类型的处理而提供的更优雅便捷的语法.

**结论**: 我起初的理解刚好搞反了, **`catch` 是全能的错误处理者, 而 `rescue`
才是挑剔者**.

## 怎么拯救

上一节我们讨论了, Elixir 中的 `rescue` 的使用场景,
现在我们来讨论如何使用它.

比如让我们使用 `try...rescue` 来来模拟 Erlang 的 maybe 特性.

```elixir
defmodule Corner.Maybe do
  defmacro maybe(do: block) do
    quote do
      try do
        unquote(block)
      rescue
        e in [MatchError] -> e.term
        # %MatchError{term: term} -> term
        e -> reraise e, __STACKTRACE__
      end
    end
  end
end
```

不难推测, 还可以在第 7 行的列表中加入更多的错误类型.
如果不管异常的名字叫什么都要处理的话, 就可以向第 9 行那样处理.

但是像第 7 行中对错误的处理, 处理的结果, 只是从错误结构中提取相应的异常消息,
那么我忍不住, 就想使用注释掉的第 8 行那样的代码.
但是第 8 行的代码是语法错误的. `rescue` 子句中, `->` 左侧的模式匹配,
只基于异常的名字来处理[^1].

这样的处理有些奇怪啊! 如果允许第 8 行那样写代码, 代码不是显的更整洁吗?
为什么 Elixir 对 `rescue` 的匹配匹配语法要限制只能匹配错误名字呢?
现在还没有答案, 但是不要这一个问题阻拦了我们探索的脚步.
也许后面的探索中, 答案自然就会浮出水面呢!

## 如何捕获

通过上面对 `rescue` 和 `catch` 的分工的讨论, 我们知道 `catch` 是三项全能.
现在让我们来看看 `catch` 的表现.

### 抛接游戏

Elixir 中 `throw` 可以抛出任意的数据类型 (就像 C++ 中那样),
而 `catch` 可以原封不动的接住所有被抛出的东西.
Elixir 中没有 `return` 语句, 使用 `throw` `catch` 可以模拟它.
例如类似于下面的 Javascript 代码:

```javascript
function fun(a){
  if(a > 3) return "a > 3"
  if(a < 0) return "a > 0"
  if(a == 2) return a
  return a+1
}
```

对应的 Elixir 代码可以这样来写:

```elixir
def fun(a) do
  if a > 3, do: throw "a > 3"
  if a < 0, do: throw "a < 0"
  if a == 2, do: throw a
  catch
    v -> v
  else
   _ -> a + 1
end
```

### 信号捕获

在 Erlang 中, `catch` 实际上更加类似于 Linux 进程间通信机制中的信号陷阱.
也就是说, `catch` 捕获的实际上是信号, Elixir 信号比 Linux 的信号通信高级的地方在于:
Linux 的信号种类是固定的; 而 Elixir 中 `exit` 可以发送的信号,
是由标签表示的**类型**与**附加消息**共同组成的. 其中标签个数可以认为是固定的,
但是附加信息的数量是无限的.

`catch` 子句中完整的匹配语法是这样的:
```elixir
catch
  kind, message when guard -> ...
```

也就是说, `catch` 子句中 `->` 左侧, 应该是一个二元的参数列表形式.
`when` 子句是可以省略的, 就像其他环境中的 `when` 那样.
而 `catch v -> ...` 是 `catch :throw, v -> ...` 的语法糖,
或者按照 Erlang 文档的说法, `catch` 的默认类型是 `:throw`.

我们用代码来检验以下我们的理解是否正确:

```elixir
1 = try do
  throw(1)
catch
  :throw, v -> v
end

2.0 = try do
  throw(2.0)
catch
  v -> v
end
```

对 `throw` 产生的错误来说, `catch v ->..` 和 `catch :throw, v -> ...` 一样.
这证实了 `catch v -> ...` 只是 `catch :throw, v -> ...` 的语法糖的结论.

```elixir
2 =
  try do
    exit(2)
  catch
    :exit, v -> v
  end
```

对 `exit` 引发的错误, 可以使用 `catch :exit, v ->` 捕获.

```elixir
3 =
  try do
    :erlang.error(3)
  catch
    :error, v -> v
  end

:reason =
  try do
    :erlang.error(:reason, [2])
  catch
    :error, reason -> reason
  end
```

对Erlang 的 `:erlang.error/1-2-3` 产生的错误, `catch` 总是能捕获其原因(代码中的变量
`reason`), 其值就是 `:erlang.error` 的第一个参数.

```elixir
:badarith =
  try do
    1 / 0
  catch
    :error, v -> v
  end

{:badmatch, :error} =
  try do
    :ok = :error
  catch
    # not work
    # :error, %MatchError{}=v -> v
    # now `reason` is not a Error, but a tuple
    :error, reason -> reason
  end
```

使用 `catch :error, reason -> ...` 来捕获运行时错误,
`reason` 绑定错误发生的原因. 不同的错误, 产生的原因数据类型不统一.
比如算术计算引发的错误, 是用一个原子 `:badarith` 表示.
而匹配错误, 返回的是一个二元元组, 元组的第一个元素是原子 `:badmatch`,
第二个元素则是匹配操作符 `=` 的右操作数.

```elixir
:reason =
  try do
    exit(:reason)
  catch
    :exit, v -> v
  end

{:kill, 1} =
  try do
    exit({:kill, 1})
  catch
    :exit, v -> v
  end
```

对于 `exit` 产生的错误, 无论传递给 `exit/1` 的是什么,
`catch :exit, v ->` 语句中 `v` 都能绑定它的值.

```elixir
%MatchError{} =
  try do
    raise MatchError
  catch
    :error, %MatchError{} = v -> v
  end

%MatchError{} =
  try do
    raise MatchError, term: {:ok, 1}
  rescue
    v in MatchError -> v
  end
```

`catch` 和 `rescue` 都可以用来处理 `raise` 引发的错误.
`catch` 的语法可以更方便的使用匹配语法, `rescue` 只能匹配模块名.

但是, 注意对比运行时产生的匹配错误与使用 `raise` 产生的匹配错误之间的微妙区别.
运行时产生的匹配错误, 使用 `catch :error, v -> ...` 语句时, `v` 绑定的是一个二元元组.
而 `rasie` 产生的错误, 表达式 `catch :error, v ->...` 中 `v` 绑定的是
`%MatchError{}` 异常结构.

## 更优雅的拯救

在探索 `catch` 对异常的捕获是, 我们发现 `catch` 对运行时错误和 `raise`
产生的错误有区分, 这使得 `catch` 对错误来源敏感, 这就导致了由不同的机制
(`error` 或 `raise`) 产生的相同异常 (`MatchError`), 必须使用不一致的语法来处理.
Elixir 中用 `rescue` 来统一处理异常, 而不再区分它们的来源.
可是 `rescue` 子句中的匹配的语法, 只支持对异常名匹配.
这样在需要提取异常消息的时候, 代码就不够简洁了.
好在变量可以匹配任意给定的值, 这个匹配规则没有打破.
所以 `rescue v ->` 可以用来匹配任意的 `:error` 类型的异常.

`rescue v -> ` 表达式中, 当变量 `v` 绑定异常结构后, 在 `->` 的右侧, 
就可以使用结构的匹配语法来对异常做匹配了.
也就是说, 对于异常, 我们可以使用下面的代码来完成匹配:
```elixir
try do
   do_block_code
rescue v ->
    case v do
     %ErrorName{field: field_v} -> field_v
     ...
    end
...
end
```

我们可以定义自己的语法糖, 让 `rescue` 子句中, 可以使用更强大的模式匹配语法.

`try` 是 Elixir 特殊表单, 社区对其语法有一致的预期,
为了不迷惑, 不熟悉我们的库的程序员, 我们不使用 `try` 来作为我们的宏的名字.
这个将要定义的宏是要增强 `rescue` 的匹配功能, 这里我们使用和 `fn!` 一样的命名惯例,
把我们的宏命名为`try!`.

```elixir
defmodule Corner.Try do
  defmacro try!(asts) do
    Macro.postwalk(asts, &walker/1)
    |> make_try()
    |> tap(&(Macro.to_string(&1) |> IO.puts()))
  end
  #...
end
```
我们只是修改 `rescue` 分句, 所以, 遍历抽象语法树, 找到 `resuce` 分句相关的部分,
修改抽象语法树, 这就是 `try!/1` 最重要的工作. 这个工作有 `walker/1` 函数完成.

`walker` 函数使用模式匹配, 识别出 `rescue` 分句, 然后调整抽象语法树对应的部分,
使得原来的 
```elixir
rescue 
  %Error_1{} -> ...
  %Error_2{} -> ...
  ...
  %Error_n{} -> ...
```
转化为:
```elixir
rescue
  v -> case v do
      %Error_1{} -> ...
      %Error_2{} -> ...
      ...
      %Error_n{} -> ...
  end
```
对应的代码, 非常简单:

```elixir
defp walker({:rescue, rescue_block}) do
  {:rescue,
   [
     {:->, [],
      [
        [{:v, [], nil}],
        {:case, [], [{:v, [], nil}, [do: rescue_block]]}
      ]}
   ]}
end

defp walker(ast), do: ast
```
最后, `try!` 还是需要使用特殊表达 `try` 来完成工作, 所以最后我们用 `make_try/1`
来完成对 `try` 特殊表单的调用表达式.
```elixir
defp make_try(block) do
  {:try, [], [block]}
end
```

最后使用我们新定义的 `super_try` 来重构我们的 `maybe` 宏.

```elixir
defmodule Corner.Maybe do
  defmacro maybe(do: block) do
    quote do
      import Corner.Super, only: [try!: 1]

      try! do
        unquote(block)
      rescue
        %MatchError{term: term} -> term
      end
    end
  end

  defmacro maybe(do: block, else: else_part) do
    quote do
      import Corner.Super, only: [try!: 1]

      try! do
        unquote(block)
      rescue
        %MatchError{term: term} -> term
      else
        unquote(else_part)
      end
    end
  end
end
```

测试一下:

```elixir
defmodule Corner.MaybeTest do
  use ExUnit.Case
  import Corner.Maybe

  test "maybe" do
    result_maybe =
      maybe do
        {:ok, a} = {:ok, 2}
        {:ok, b} = {:error, a + 1}
        b + 1
      end

    assert result_maybe == {:error, 3}
  end
end
```

## 错误处理子句的顺序

最后, 讨论一下 `try` 结构中子句的顺序. 在 Elixir 文档中, 最完整的错误处理块为
`try` ... `rescue` ... `catch` ... `else` ... `after`.
我们看到的绝大部分例子, 都是省略部分子句的情况,
但是这些子句的相对顺序还是保持的, 那么能不能改变这些子句的相对顺序呢?
比如, 把 `rescue` 放在 `catch` 后.

```elixir
try do
  raise :error
catch
  v -> v |> IO.inspect(label: "catch v")
rescue
  v -> v |> IO.inspect(label: "rescue")
else
  v -> v |> IO.inspect(label: "else")
after
  IO.puts("after")
end
```
上面的代码, 编译器给出的警告, 警告的内容是 `catch` 应该放在 `resuce` 的后面,
不过代码依旧可以运行. 那么为什么应该那样呢? 前面的讨论, 我们知道 `catch` 是全能捕手,
上面的代码中, 用到的是它捕获 `throw value` 的语法,
所以`rescue` 有机会捕获 `raise` 产生的错误. 但是如果使用的最通用 `catch` 的语法呢?
例如下面的代码:
```elixir
catch
  :error, v -> ...
rescue
  rescue_code
```

在上面的代码中, `rescue` 子句就是死代码, 永远不用有机会执行. 编译器给出警告的原因.

那么除了 `rescue` 和 `catch` 的相对位置外, 其他的子句呢? 能改变顺序码?
比如, 把 `after` 子句放在 `resuce` 前面, 可以吗?

```elixir
try do
  1 / 0
after
  IO.puts("after")
rescue
  v ->
    v |> IO.inspect(label: "rescue")
    :rescue
catch
  v ->
    v |> IO.inspect(label: "catch")
    :catch
else
  v -> v |> IO.inspect(label: "else")
end
```

上面的代码片段, 不但能顺利通过编译, 而且警告都没有. 也就是说, 这是完全合法的代码.
`after` 在代码中出现的顺序, 不影响其功能. 进一步的, 不难推测,
`else` 也可以放在 `resuce` 子句前.

```elixir
try do
  2
rescue
  v -> IO.inspect(v, label: "resuce v")
else
  v -> v |> IO.inspect(label: "else")
catch
  :error, v ->
    IO.inspect(v, label: "catch :error ,v")
end
```
上面的代码, 也一切正常.

总结一下: 在 `try` 子句中: 如果 `rescue` 和 `catch` 都存在的话,
`rescue` 应该放在 `catch` 子句的前面, 而其他的子句的顺序并没有特别的要求.

最后提醒读者, 我们这里是对语法规则的探索, 才故意打乱其顺序的,
绝对不是鼓励大家在实际的编码中也这样做.

## 什么时候必须 try

在 Elixir 的官方文档中, 我们看到可以省略 `try` 关键字, 而直接处理错误的例子.
例如在命名函数中:
```elixir
def fun() do
   ...
catch
  :error, v -> v
after
  ...
end
```

那么什么时候, 可以省略 `try` 呢? 是不是所有后面需要 `do-block` 的语法结构中,
都可以使用省略 `try` 而直接使用错误处理子句呢? 不是的. 例如,
我们就不可以给 `if` 语句加一个 `catch` 子句. 就像下面的代码这样.

```elixir
if 3 < 4 do
  1 -> 1 / 0
catch
  :error, _ -> "1/0"
else
  "how it can be 3 not less than 4"
end
```

我们看到命名函数中, 可以直接使用错误处理的子句, 那么是不是在匿名函数中也可以直接使用呢?
答案也是否定的, 而且 Elixir ≪语法参考≫文档的
[≪保留字≫](https://hexdocs.pm/elixir/1.13.1/syntax-reference.html#reserved-words)
小节也给出了提示:

> `do`, `end`, `catch`, `rescue`, `after`, `else` 在 `do-end` 块中使用.

`fn` 的语法是这样的:
```elixir
fn a -> ... end
```

当这里的 `...` 表示多于一个表达式的时候, 这里实际上是一个暗含的块, 但是并不是一个
`do-block`. 这一点我们可以通过 `quote` 语句, 在抽象语法树的层面上看清.

```elixir
quote do
  fn a ->
    a + 1
    a - 1
  end

  def f(a) do
    a + 1
    a - 1
  end
end
```

`fn` 的块结构抽象语法树是:
```elixir
{:__block__, [],
  [
    {:+, [context: Elixir, import: Kernel], [{:a, [], Elixir}, 1]},
    {:-, [context: Elixir, import: Kernel], [{:a, [], Elixir}, 1]}
  ]}
```

而函数 `f(a)` 定义的 `do...end` 块的抽象语法树是:
```elixir
[do: {:__block__, [],
  [
    {:+, [context: Elixir, import: Kernel], [{:a, [], Elixir}, 1]},
    {:-, [context: Elixir, import: Kernel], [{:a, [], Elixir}, 1]}
  ]}]
```
知道了这些知识, 就不难理解为什么 `fn` 定义匿名函数的时候, `try` 不能省略了.

接着探索会发现, `def/2`, `defp/2`, `defmacro/2` 和 `defmacrop` 中可以省略 `try`.
其他情形下对 `try` 的省略,  都是对这个特定规则的过度推广.

[^1]: Elixir 文档中关于 `rescue` 匹配的文档, 翻译过来是: 除了以模式匹配外,
`rescue` 围绕异常提供了一些方便, 允许我们根据异常的名字来展开救援.
原文见 Elixir 文档关于
[rescue 子句的描述](https://hexdocs.pm/elixir/1.13.1/Kernel.SpecialForms.html#try/1-rescue-clauses).

